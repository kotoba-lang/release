(ns kotoba.release.cli-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.release.cli :as cli]))

(def policy
  {:release-tags
   {:prefix "v"
    :binds #{:version :commit :tree :source-root :issued-at-ms
             :language-profile :package-contract :artifact-digests
             :conformance-result}}})

(def unsigned
  {:tag "v0.7.0" :version "0.7.0" :language-profile 6
   :package-contract 1 :commit "abc" :tree "def"
   :source-root "sha256:source" :issued-at-ms 1786406400000
   :artifact-digests {:darwin-arm64 "sha256:artifact"}
   :conformance-result {:status :passed :tests 1 :assertions 1}})

(deftest offline-sign-and-external-trust-verification
  (let [seed (byte-array (map unchecked-byte (range 32)))
        signer (cli/signer-from-seed seed)
        trust {:signers {signer {:status :active}}}
        signed (cli/sign-envelope policy trust unsigned seed)]
    (is (:valid? (cli/verify-envelope policy trust signed)))
    (testing "artifact tampering fails"
      (is (= :tag/invalid-signature
             (:code (cli/verify-envelope
                     policy trust
                     (assoc-in signed [:artifact-digests :darwin-arm64]
                               "sha256:attacker"))))))
    (testing "revoked signer fails"
      (is (= :tag/signer-untrusted
             (:code (cli/verify-envelope
                     policy {:signers {signer {:status :revoked}}} signed)))))))

(deftest signer-and-signature-cannot-be-prepopulated
  (let [seed (byte-array (map unchecked-byte (range 32)))]
    (is (= :release/prepopulated-signature
           (:code (ex-data
                   (try
                     (cli/sign-envelope policy {} (assoc unsigned :signer "did:key:x") seed)
                     (catch Exception e e))))))))
